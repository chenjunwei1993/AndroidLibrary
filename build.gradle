// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'build_config.gradle'
apply from: 'build_precfg.gradle'
apply from: 'build_maven.gradle'

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        //kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath "org.greenrobot:greendao-gradle-plugin:$greendao"
    }
    //gradle构建时用utf-8
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

//强制依赖版本
subprojects {
    project.configurations.all {
        resolutionStrategy.force 'com.android.support:multidex:1.0.2'
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains("multidex")) {
                details.useVersion "$Support_V7"
            }
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion "$kotlin_version"
            }
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url rootProject.MavenHostLibrary }
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
