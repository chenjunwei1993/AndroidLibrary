apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven'
apply plugin: 'org.greenrobot.greendao'


String moduleName = "LibraryGreenDao"

android {
    compileSdkVersion rootProject.compileSdkVersion
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        buildConfigField "Boolean", "DEBUG_MODE", rootProject.debugMode
    }

    // 忽略校验png格式图片
    aaptOptions {
        cruncherEnabled = false
    }

    //取消检查
    lintOptions {
        abortOnError false
    }

    greendao {
        //数据库版本
        schemaVersion 1
        //设置DaoMaster 、DaoSession、Dao包名
        daoPackage 'com.android.library.dao'
        //greenDAO生成的DAOMaster和DaoSession的位置
        targetGenDir 'src/main/java'
    }

    //上传源码后可查看注释
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }
    artifacts {
        archives sourcesJar
    }

    uploadArchives {
        repositories.mavenDeployer {
            repository(url: rootProject.MavenHostLibrary) {
                authentication(userName: rootProject.MavenUserName, password: rootProject.MavenPWD)
            }
            pom.groupId = rootProject.MavenGroupIdLibrary
            pom.packaging = rootProject.MavenPacking
            pom.artifactId = moduleName
            pom.version = "${LibraryGreenDao}"
        }
    }

    //设置输出aar文件名称
    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = moduleName + rootProject.preVersion + '.aar'
        }
    }

    //下面看情况添加，一般不用，添加上面的apply 就可以了
    kapt {
        generateStubs = true
    }

}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api "org.greenrobot:greendao-generator:$greendao"
    api "org.greenrobot:greendao:$greendao"
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "net.zetetic:android-database-sqlcipher:$sqlcipher"
}

